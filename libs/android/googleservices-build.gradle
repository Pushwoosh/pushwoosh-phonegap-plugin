buildscript {
    repositories {
        mavenCentral()
        jcenter()
	google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:+'
        classpath 'com.google.gms:google-services:4.3.8'
    }
}

String getVersion() {
  def compileSdkVersion = Integer.parseInt(project.android.compileSdkVersion.split("-")[1])
  return compileSdkVersion >= 28 ? "28.0.0" : "27.+" 
}

ext.pushwoosh = "6.4.2"

// Defer the definition of the dependencies to the end
// of the "configuration" phase from the app build.gradle file
// so that we can inquire the proper compile sdk version being used.
cdvPluginPostBuildExtras.push({
  dependencies {
    def supportVersion = getVersion()
    implementation "com.google.firebase:firebase-core:(+,19.0.99]"
    implementation "com.google.firebase:firebase-messaging:(+,22.0.99]"
    implementation "com.android.support:support-v4:${supportVersion}"
    implementation "com.android.support:appcompat-v7:${supportVersion}"
    implementation "com.android.support:recyclerview-v7:${supportVersion}"
    implementation "com.android.support:design:${supportVersion}"
    implementation "com.android.support.constraint:constraint-layout:1.0.2"
    implementation "com.github.bumptech.glide:glide:4.7.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:1.1.60"
    implementation "com.pushwoosh:pushwoosh:${pushwoosh}"
    implementation "com.pushwoosh:pushwoosh-firebase:${pushwoosh}"
    implementation "com.pushwoosh:pushwoosh-amazon:${pushwoosh}"
    implementation "com.pushwoosh:pushwoosh-badge:${pushwoosh}"
    implementation "com.pushwoosh:pushwoosh-inbox:${pushwoosh}"
    implementation "com.pushwoosh:pushwoosh-inbox-ui:${pushwoosh}"
    implementation "com.pushwoosh:pushwoosh-huawei:${pushwoosh}"
  }
})

// use postBuildExtras to make sure the plugin is applied after
// cdvPluginPostBuildExtras. Therefore if googleServices is added
// to cdvPluginPostBuildExtras somewhere else, the plugin execution
// will be skipped and project build will be successfull
ext.postBuildExtras = {
    if (project.extensions.findByName('googleServices') == null) {
        // apply plugin: 'com.google.gms.google-services'
        // class must be used instead of id(string) to be able to apply plugin from non-root gradle file
        apply plugin: com.google.gms.googleservices.GoogleServicesPlugin
    }
}
